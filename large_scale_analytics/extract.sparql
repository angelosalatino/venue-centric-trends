prefix sg:<http://scigraph.springernature.com/ontologies/core/>
prefix grid:<http://www.grid.ac/ontology/>

# ANOMALIES found:
# Author multiple affiliation, DOI '10.1007/978-3-540-68234-9_5'
# Non-existing organization in GRID, DOI '10.1007/978-3-540-30103-5_26'
# Pending affiliation, DOI "10.1007/978-0-387-35503-0_29"

select ?doi (concat('[',?authorsPivot,']') as ?authors) ?title ?confName ?confSubtitle ?rights ?year {
	select ?doi (GROUP_CONCAT(?authorStruct; separator=',') as ?authorsPivot) ?title ?confName ?confSubtitle ?rights ?year {
		select ?doi (concat('{"order":"',str(?order),'","given":"',?authorGivenName,'","family":"',?authorFamilyName,'","affiliation":',?authorAffiliations,'}') as ?authorStruct) ?title ?confName ?confSubtitle ?rights ?year {
			select ?doi ?order ?authorGivenName ?authorFamilyName (concat('[',?authorAffiliationsPivot,']') as ?authorAffiliations) ?title ?confName ?confSubtitle ?rights ?year {
				select ?doi ?order ?authorGivenName ?authorFamilyName (GROUP_CONCAT(?orgStruct; separator=',') as ?authorAffiliationsPivot) ?title ?confName ?confSubtitle ?rights ?year {
					select ?doi ?order ?authorGivenName ?authorFamilyName (concat('{"grid":"',?gridId,'","name":"',?orgName,'","city":"',?city,'","country":"',?country,'","code":"',?code,'"}') as ?orgStruct) ?title ?confName ?confSubtitle ?rights ?year
					where {
						?edition sg:title ?confName ;
							sg:subtitle ?confSubtitle ;
							sg:bookType "Proceedings" .
						?chapter sg:hasBookEdition ?edition ;
							sg:doi ?doi ;
							sg:title ?title ;
							sg:hasContribution ?author ;
							sg:copyrightYear ?year .
                        optional {
                            ?chapter sg:bodyPdfRights ?rights .
                        }
						?author sg:publishedFamilyName ?authorFamilyName ;
							sg:publishedGivenName ?authorGivenName ;
							sg:hasAffiliation ?affiliation ;
							sg:order ?order .
						optional {
							?affiliation sg:hasOrganization ?org .
							?org grid:id ?gridId ;
								rdfs:label ?orgName ;
								grid:hasAddress ?orgAddr .
							?orgAddr grid:countryName ?country ;
								grid:cityName ?city ;
							grid:countryCode ?code .
						}
					}
				}
				group by ?doi ?order ?authorGivenName ?authorFamilyName ?title ?confName ?confSubtitle ?rights ?year
			}
		}
	}
	group by ?doi ?title ?confName ?confSubtitle ?rights ?year
}
